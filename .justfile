####################################################################################################

_default:
  @just --choose

####################################################################################################

@abort:

####################################################################################################

@list:
  just --list

####################################################################################################

# print justfile
[group('just')]
@show:
  bat .justfile --language make

####################################################################################################

# edit justfile
[group('just')]
@edit:
  micro .justfile

####################################################################################################

# compile roadmap action items
[group('dev')]
@todor:
  if [[ ! -f 'ROADMAP.txt' ]]; then touch 'ROADMAP.txt'; fi
  sed -i '' '/^==================================================$/,/^==================================================$/d' ROADMAP.txt
  { printf '==================================================\n'; todor -s; printf '==================================================\n'; } >> ROADMAP.txt

####################################################################################################
# config
####################################################################################################

goapp := 'Babel'
goexe := 'babel'
dir   := '.babel'
config := dir / 'config'

####################################################################################################
# jobs
####################################################################################################

# build exec
[group('go')]
build app=goapp:
  @echo "\n\033[1;33mBuilding\033[0;37m...\n=================================================="
  go build -v -o excalibur/{{app}}

####################################################################################################

# install locally
[group('go')]
install app=goapp exe=goexe:
  @echo "\n\033[1;33mInstalling\033[0;37m...\n=================================================="
  go install
  @echo "\n\033[1;33mLinking\033[0;37m...\n=================================================="
  mv -v "${HOME}/go/bin/{{app}}" "${HOME}/go/bin/{{exe}}"
  @echo "\n\033[1;33mCopying\033[0;37m...\n=================================================="
  @if [ ! -d "${HOME}/{{dir}}" ]; then mkdir "${HOME}/{{dir}}"; fi
  @if test -e "${HOME}/{{config}}"; then rm -r "${HOME}/{{config}}"; fi && echo "\033[1;33mconfig\033[0;37m" && cp -v -R "config" "${HOME}/{{config}}"

####################################################################################################

# watch changes
[group('go')]
watch:
  watchexec --clear --watch cmd -- 'just install'

####################################################################################################

# print help
[group('go')]
@help:
  @echo "\n\033[1;33mPrinting\033[0;37m...\n=================================================="
  go run -- --help

####################################################################################################
